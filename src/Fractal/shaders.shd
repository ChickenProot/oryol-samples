//------------------------------------------------------------------------------
//  shaders.shd
//------------------------------------------------------------------------------

@block util
vec4 hue2rgb(float h) {
    float r = abs(h * 6.0 - 4.0) - 1.0;
    float g = 2.0 - abs(h * 6.0 - 2.0);
    float b = 2.0 - abs(h * 6.0 - 4.0);
    return vec4(r, g, b, 1.0);
}
@end

//------------------------------------------------------------------------------
@vs FractalVS
uniform vsParams {
    vec4 rect;
};
in vec4 position;
in vec2 texcoord0;
out vec2 uv;
out vec2 pos;

void main() {
    gl_Position = position;
    uv = texcoord0;
    pos = rect.xy + (texcoord0 * (rect.zw - rect.xy));
}
@end

@fs MandelbrotFS
uniform sampler2D tex;
in vec2 uv;
in vec2 pos;
out vec4 fragColor;

void main() {
    // read previous params
    vec4 params = texture(tex, uv);
    float cr = params.x;
    float ci = params.y;
    float it = params.w;
    float d = cr*cr + ci*ci;
    if (d > 4.0) {
        // done
        fragColor = vec4(cr, ci, 1.0, it);
    }
    else {
        float cr_new = cr*cr - ci*ci + pos.x;
        float ci_new = 2.0 * cr*ci + pos.y;
        fragColor = vec4(cr_new, ci_new, 0.0, it + 1.0);
    }
}
@end

@fs JuliaFS
uniform fsParams {
    vec2 juliaPos;
};
uniform sampler2D tex;

in vec2 uv;
in vec2 pos;
out vec4 fragColor;

void main() {
    vec4 params = texture(tex, uv);
    float cr = params.x;
    float ci = params.y;
    float it = params.w;
    float d = cr*cr + ci*ci;
    if (d > 4.0) {
        fragColor = vec4(cr, ci, 1.0, it);
    }
    else {
        if (it < 1.0) {
            cr = pos.x;
            ci = pos.y;
        }
        float cr_new = cr*cr - ci*ci + juliaPos.x;
        float ci_new = 2.0 * cr*ci + juliaPos.y;
        fragColor = vec4(cr_new, ci_new, 0.0, it + 1.0);
    }
}
@end

//------------------------------------------------------------------------------
@vs DisplayVS
in vec4 position;
in vec2 texcoord0;
out vec2 uv;

void main() {
    gl_Position = position;
    uv = texcoord0;
}
@end

@fs DisplayFS
@include util
uniform fsParams {
    float numColors;
};
uniform sampler2D tex;

in vec2 uv;
out vec4 fragColor;

void main() {
    vec4 params = texture(tex, uv);
    if (params.z > 0.5) {
        float i = params.w;
        float hue = mod(i, numColors) / numColors;
        fragColor = hue2rgb(hue);
    }
    else {
        fragColor = vec4(0.0, 0.0, 0.0, 1.0);
    }
}
@end

@program Mandelbrot FractalVS MandelbrotFS
@program Julia FractalVS JuliaFS
@program DisplayShader DisplayVS DisplayFS
